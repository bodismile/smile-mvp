apply plugin: 'com.android.library'

// JitPack Maven
apply plugin: 'com.github.dcendents.android-maven'
// Your Group
group='com.github.bodismile'

android {
    compileSdkVersion 22
    buildToolsVersion '22.0.1'

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 22
        versionCode 1
        versionName "1.0.1"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    sourceSets {
        main {
            jniLibs.srcDirs = ['libs']//将so文件目录指向libs目录
        }
    }

    lintOptions {
        abortOnError false
    }

    compileOptions {
        encoding "UTF-8"
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

}

repositories{
    flatDir {
        dirs 'libs'
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
    compile 'com.android.support:appcompat-v7:22.2.0'
    compile 'com.android.support:support-v4:22.2.0'
    provided 'com.android.support:recyclerview-v7:22.2.0'
    //视图加载
    compile 'com.jakewharton:butterknife:7.0.1'
    //rx相关
    compile 'io.reactivex:rxjava:1.1.6'
    compile 'io.reactivex:rxandroid:1.2.0'
    //友盟统计
    compile 'com.umeng.analytics:analytics:latest.integration'
    //图片加载-glide
    provided 'com.github.bumptech.glide:glide:3.7.0'
    //图片加载-uil
    provided 'com.nostra13.universalimageloader:universal-image-loader:1.9.5'
    //pulltorefresh下拉刷新
    provided 'com.github.bodismile:smile-pulltorefresh:1.0.0'
    //BGARefresh下拉刷新
    provided 'com.nineoldandroids:library:2.4.0'
    provided 'cn.bingoogolapple:bga-refreshlayout:1.1.6@aar'
    provided 'cn.bingoogolapple:bga-adapter:1.1.0@aar'
    //adapter
    compile 'io.nlopez.smartadapters:library:1.3.1'
    //jsbridge
    compile 'com.github.lzyzsd:jsbridge:1.0.4'
}

//-------------------------------------------

// 指定编码
tasks.withType(Javadoc) {
    options.encoding = "UTF-8"
}

// 打包源码
task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    failOnError  false
    source = android.sourceSets.main.java.sourceFiles
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    classpath += configurations.compile
}

// 制作文档(Javadoc)
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

//指定javadoc编码格式
javadoc {
    options {
        encoding "UTF-8"
        charSet 'UTF-8'
        author true
        version true
        links "http://docs.oracle.com/javase/7/docs/api"
    }
}

artifacts {
    archives sourcesJar
    archives javadocJar
}
